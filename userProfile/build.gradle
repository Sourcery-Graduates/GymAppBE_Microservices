plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.google.cloud.tools.jib' version '3.4.5'
}

group = 'com.sourcery.gymapp'
version = '1.0.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

jib {
	to {
		image = "sourcerygraduates/gymapp-${project.name.toLowerCase()}"
	}
	container {
		jvmFlags = [
				"-Dspring.profiles.active=local",
		]
		environment = [
				"CONFIG_SERVER_URI" : "http://host.docker.internal:8888"
		]
		ports = ["8083"]
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator:3.4.0'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'software.amazon.awssdk:s3:2.30.16'
	implementation 'org.apache.kafka:kafka-streams'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.1'
	implementation 'org.springframework.cloud:spring-cloud-config-client:4.2.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation "org.testcontainers:kafka:1.20.4"
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform {
		excludeTags 'integration'
	}
	systemProperty 'spring.profiles.active', 'test'
}

tasks.register('integrationTest', Test) {
	useJUnitPlatform {
		includeTags 'integration'
	}
	description = 'Runs integration tests.'
	group = 'verification'
	shouldRunAfter test
	systemProperty 'spring.profiles.active', 'test'
}

tasks.named('check') {
	dependsOn(tasks.named('integrationTest'))
}
